/* ========================================================================== *
 * TEST-NG SNIPPET                                                            *
 * -------------------------------------------------------------------------- *
 *                                                                            *
 * Dependencies and configuration for TestNG tests.                           *
 *                                                                            *
 * ========================================================================== */

// Configure for Java
if (plugins.hasPlugin('java')) {

  dependencies {
    testCompile 'org.testng:testng:6.8.21'
  }

  test {
    useTestNG();
    testLogging {
      events = [ "started", "skipped", "failed", "passed" ]
      showStandardStreams = true
      stackTraceFilters = []
      exceptionFormat = 'full'
      showStackTraces = true
      showExceptions = true
      showCauses = true
    }
  }
}

// Configure for Android
if (plugins.hasPlugin('com.android.application') ||
    plugins.hasPlugin('com.android.library')) {

  dependencies {
    androidTestCompile 'de.lemona.android:android-testng:1.1.22'
  }

  android {
    defaultConfig {
      testInstrumentationRunner 'de.lemona.android.testng.TestNGRunner'
    }
  }
}
