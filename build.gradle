plugins {
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.12.0'
    id 'de.lemona.gradle' version '0.2.6'
}

apply plugin: 'groovy'
apply plugin: 'maven-publish'

// IDE
apply plugin: 'eclipse'
apply plugin: 'idea'

// Depend on Gradle API
dependencies {
    implementation localGroovy()
}

// Disabling upload for SNAPSHOT builds
gradle.taskGraph.whenReady { taskGraph ->
    if (version.isSnapshot) {
        logger.lifecycle('Disabling publishing for snapshot')
        tasks.publishPlugins.enabled = false
    }
}

// Plugin details
pluginBundle {
    website = 'https://www.leomo.io/'
    vcsUrl = 'https://github.com/LemonadeLabInc/gradle-snippets'
    tags = ['leomo']
}

gradlePlugin {
    plugins {
        gradlePlugin {
            id = 'de.lemona.gradle'
            implementationClass = 'de.lemona.gradle.plugins.GradlePlugin'
            displayName = 'LEOMO Gradle plugin'
            description = 'LEOMO Android and Java conventions plugin'
        }
        androidPlugin {
            id = 'de.lemona.gradle.android'
            implementationClass = 'de.lemona.gradle.plugins.AndroidPlugin'
            displayName = 'LEOMO Android plugin'
            description = 'A plugin for building Android app or library. This plugin depends on de.lemona.gradle and Android plugins (com.android.application or com.android.library).'
        }
        javaPlugin {
            id = 'de.lemona.gradle.java'
            implementationClass = 'de.lemona.gradle.plugins.JavaPlugin'
            displayName = 'LEOMO Java plugin'
            description = 'A plugin for building Java library. This plugin depends on de.lemona.gradle and Java plugins (java or java-library).'
        }
        publishPlugin {
            id = 'de.lemona.gradle.publish'
            implementationClass = 'de.lemona.gradle.plugins.PublishPlugin'
            displayName = 'LEOMO Publishing plugin'
            description = 'A plugin for publishing. This plugin depends on de.lemona.gradle and maven-publish.'
        }
        s3Plugin {
            id = 'de.lemona.gradle.s3'
            implementationClass = 'de.lemona.gradle.plugins.S3Plugin'
            displayName = 'LEOMO S3 Repository plugin'
            description = 'A plugin for using AWS S3 as a maven repository. This plugin depends on de.lemona.gradle and if you\'d like to publish to S3 also depends on maven-publish.'
        }
    }
}
